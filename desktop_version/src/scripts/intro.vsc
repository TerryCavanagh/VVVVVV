ifskip(quickstart)
# createcrewman(232,113,cyan,0,faceright)
createcrewman(96,177,green,0,faceright)
createcrewman(122,177,purple,0,faceleft)
fadein()
untilfade()
delay(90)

flash(5)
shake(20)
playef(9)
musicfadeout()
changemood(player,1)
delay(15)
squeak(player)
text(cyan,0,0,1)
Uh oh...
position(player,above)
# backgroundtext
speak_active

squeak(purple)
changeai(purple,followposition,175)
text(purple,145,150,1)
Is everything ok?
# position(purple,above)
# backgroundtext
speak_active

squeak(player)
walk(left,2)
text(cyan,0,0,2)
No! We've hit some
kind of interference...
position(player,above)
# backgroundtext
speak_active

# delay(30)
endtext

flash(5)
shake(50)
playef(9)
changemood(green,1)
changemood(purple,1)
alarmon

changedir(player,1)
delay(30)
endtext

squeak(player)
text(cyan,0,0,2)
Something's wrong! We're
going to crash!
position(player,above)
# backgroundtext
speak_active
# delay(100)

endtext

flash(5)
shake(50)
playef(9)
changeai(green,followposition,-60)
changeai(purple,followposition,-60)
squeak(player)
text(cyan,70,140,1)
Evacuate!
backgroundtext
speak_active
walk(left,35)

endtextfast

# Ok, next room!

flash(5)
shake(50)
playef(9)
gotoroom(3,10)
gotoposition(310,177,0)
createcrewman(208,177,green,1,followposition,120)
createcrewman(240,177,purple,1,followposition,120)
createcrewman(10,177,blue,1,followposition,180)

squeak(blue)
text(blue,80,150,1)
Oh no!
backgroundtext
speak_active
walk(left,20)

endtextfast

# and the next!
flash(5)
shake(50)
playef(9)
gotoroom(3,11)
gotoposition(140,0,0)

createcrewman(90,105,green,1,followblue)
createcrewman(125,105,purple,1,followgreen)
createcrewman(55,105,blue,1,followposition,-200)

createcrewman(120,177,yellow,1,followposition,-200)
createcrewman(240,177,red,1,faceleft)

delay(5)
changeai(red,followposition,-200)

squeak(red)
text(red,100,150,1)
Everyone off the ship!
backgroundtext
speak_active

walk(left,25)

endtextfast

# final room:
flash(5)
shake(80)
playef(9)
gotoroom(2,11)
gotoposition(265,153,0)

createcrewman(130,153,blue,1,faceleft)
createcrewman(155,153,green,1,faceleft)
createcrewman(180,153,purple,1,faceleft)
createcrewman(205,153,yellow,1,faceleft)
createcrewman(230,153,red,1,faceleft)


squeak(yellow)
text(yellow,0,0,1)
This shouldn't be happening!
position(yellow,below)
backgroundtext
speak_active

activateteleporter()

delay(10)
changecolour(blue,teleporter)
delay(10)
changecolour(green,teleporter)
delay(10)
changecolour(purple,teleporter)
delay(10)
changecolour(yellow,teleporter)
delay(10)
changecolour(red,teleporter)
delay(10)

# and teleport!
endtext
alarmoff
flash(5)
shake(20)
playef(10)
blackout()
changemood(player,0)
changedir(player,1)

delay(100)
blackon()
shake(20)
playef(10)

# Finally, appear at the start of the game:
gotoroom(13,5)
gotoposition(80,96,0)
walk(right,20)
# delay(45)

squeak(player)
text(cyan,0,0,1)
Phew! That was scary!
position(player,above)
speak_active

squeak(player)
text(cyan,0,0,2)
At least we all
escaped, right guys?
position(player,above)
speak_active

endtext

delay(45)
walk(left,3)
delay(45)
setcheckpoint()

squeak(player)
text(cyan,0,0,1)
...guys?
position(player,above)
speak_active

endtext

delay(25)
changemood(player,1)
squeak(cry)
delay(25)

play(1)
endcutscene()
untilbars()

hideship()

gamestate(4)